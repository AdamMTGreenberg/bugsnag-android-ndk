buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle-experimental:0.8.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

// Tasks to build and test the android library
apply plugin: 'com.android.model.library'
model {
    android {
        compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
        buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

        defaultConfig {
            minSdkVersion.apiLevel 9
            targetSdkVersion.apiLevel Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        }

        ndk {
            moduleName "bugsnag-ndk"
            ldLibs.addAll(['log'])
            platformVersion = 21 // Note this must be >=21 for 64 bit architectures
            abiFilters.addAll(["armeabi", "armeabi-v7a", "x86", "mips", "arm64-v8a", "x86_64", "mips64"])
            CFlags.add("-std=c99")
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.android.support:support-annotations:25.0.0"
    compile 'com.bugsnag:bugsnag-android:3.8.0'
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        Publication(MavenPublication) {
            artifact file('build/outputs/aar/bugsnag-android-ndk-release.aar')
            groupId 'com.bugsnag'
            artifactId 'bugsnag-android-ndk'
            version project.VERSION_NAME
        }
    }
    repositories {
        mavenCentral()
    }
}
// Bintray upload
bintray {
    user = "$bintray_user"
    key = "$bintray_api_key"
    publications = ['Publication']
    pkg {
        repo = 'maven'
        name = 'bugsnag-android-ndk'
        userOrg = 'bugsnag'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/bugsnag/bugsnag-android-ndk.git'
        version {
            name = project.VERSION_NAME
            vcsTag = "v${project.VERSION_NAME}"
        }
    }
}

