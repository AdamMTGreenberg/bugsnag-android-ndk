apply plugin: 'com.android.application'

repositories {
    jcenter()
    google()
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.bugsnag.android.ndk.test"
        minSdkVersion Integer.parseInt(project.ANDROID_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_TARGET_SDK_VERSION)

        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        myConfig {
            storeFile file("../bugsnag-ndk-test.jks")
            storePassword "bugsnagtest"
            keyAlias "bugsnag"
            keyPassword "bugsnagtest"
            storeType "jks"
        }
    }

    flavorDimensions "regular"

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles.add(file('proguard-rules.pro'))
            proguardFiles.add(getDefaultProguardFile('proguard-android.txt'))
            signingConfig signingConfigs.myConfig
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }

//    productFlavors {
//        x86 {
//            ndk {
//                abiFilter "x86"
//            }
//        }
//        arm {
//            ndk {
//                abiFilters "armeabi-v7a", "armeabi"
//            }
//        }
//    }

}

task installBugsnagJNILibs << {
    def configs = project.configurations.findAll {
        it.toString().contains('CompileClasspath')
    }.each { config ->
        def artifactFile = config.resolvedConfiguration.getFiles().find {
            it.toString().contains("bugsnag-android-ndk")
        }
        if (artifactFile) {
            copy {
                from zipTree(artifactFile)
                into file("${buildDir}/intermediates/bugsnag-libs")
//                eachFile { it.path = it.path.replaceFirst("^jni", "") }
            }
        }
    }
}

project.afterEvaluate {
    project.tasks.findAll {
        it.name.startsWith("externalNative")
    }
    .forEach {
        it.finalizedBy installBugsnagJNILibs
    }
}

dependencies {
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.bugsnag:bugsnag-android-ndk:1.0.1"
    implementation "com.bugsnag:bugsnag-android:4.1.4"
}

apply plugin: 'com.bugsnag.android.gradle'
bugsnag {
    overwrite true
    retryCount 3
    ndk true
}
