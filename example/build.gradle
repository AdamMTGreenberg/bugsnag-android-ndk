apply plugin: 'com.android.application'

repositories {
    jcenter()
    google()
}

allprojects {
    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.bugsnag.android.ndk.test"
        minSdkVersion Integer.parseInt(project.ANDROID_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_TARGET_SDK_VERSION)

        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        myConfig {
            storeFile file("../bugsnag-ndk-test.jks")
            storePassword "bugsnagtest"
            keyAlias "bugsnag"
            keyPassword "bugsnagtest"
            storeType "jks"
        }
    }

    flavorDimensions "regular"

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles.add(file('proguard-rules.pro'))
            proguardFiles.add(getDefaultProguardFile('proguard-android.txt'))
            signingConfig signingConfigs.myConfig
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }

    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }
        }
        arm {
            ndk {
                abiFilters "armeabi-v7a", "armeabi"
            }
        }
    }
}

dependencies {
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.bugsnag:bugsnag-android-ndk:1.1.2"
    implementation "com.bugsnag:bugsnag-android:4.2.2"
//    compile (name: "ndk-release", ext: "aar")

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'


    androidTestImplementation "com.android.support.test:runner:$supportTestVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test:rules:$supportTestVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

apply plugin: 'com.bugsnag.android.gradle'
bugsnag {
    overwrite true
    retryCount 3
    ndk true
}

apply from: "../checkstyle.gradle"
